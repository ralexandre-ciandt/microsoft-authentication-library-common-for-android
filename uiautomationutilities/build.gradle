apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    def final installSourcePlayStore = "PlayStore"
    def final installSourceLocalApk = "LocalApk"

    // defaults to LocalApkFile - the file must be pushed to the device before tests
    def brokerInstallationSource = installSourceLocalApk

    if (project.hasProperty("brokerSource")) {
        if (brokerSource.equalsIgnoreCase(installSourcePlayStore)) {
            brokerInstallationSource = installSourcePlayStore
        } else if (brokerSource.equalsIgnoreCase(installSourceLocalApk)) {
            brokerInstallationSource = installSourceLocalApk
        } else {
            throw new InvalidUserDataException("Unspported broker source provided: "
                    + brokerSource
                    + " Only the following broker sources are supported: PlayStore , LocalApk");
        }
    }


    defaultConfig {
        minSdkVersion rootProject.ext.automationAppMinSDKVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "BROKER_INSTALL_SOURCE", "\"$brokerInstallationSource\"")
        buildConfigField("String", "INSTALL_SOURCE_PLAY_STORE", "\"$installSourcePlayStore\"")
        buildConfigField("String", "INSTALL_SOURCE_LOCAL_APK", "\"$installSourceLocalApk\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:$rootProject.ext.appCompatVersion"
    implementation 'junit:junit:4.13'
    implementation "androidx.test.uiautomator:uiautomator:$rootProject.ext.uiAutomatorVersion"
    implementation "androidx.test:monitor:$rootProject.ext.androidxTestMonitorVersion"
    implementation "androidx.test:core:$rootProject.ext.androidxTestCoreVersion"
    implementation project(':testutils')
    compileOnly "org.projectlombok:lombok:$rootProject.ext.lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$rootProject.ext.lombokVersion"
    androidTestImplementation "androidx.test:runner:$rootProject.ext.androidxTestRunnerVersion"
}
